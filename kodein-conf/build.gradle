apply plugin: 'kotlin'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.github.dcendents.android-maven'

sourceSets {
    main.kotlin.srcDirs += "$rootDir/shared/conf/kotlin"
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile project(':kodein-core')

    testCompile "junit:junit:$junit_version"

//    testCompile project(':kodein-core').sourceSets.test.output
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from dokka
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask(tasks.uploadArchives) }
    sign configurations.archives
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    includes = ['dokka-module.md', 'dokka-package.md']

    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/SalomonBrys/Kodein/blob/master/kodein/src/main/kotlin"
        suffix = "#L"
    }
}

if (hasProperty('ossrhUsername') && hasProperty('ossrhPassword')) {
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'Kodein-Conf'
                    packaging 'jar'
                    artifactId 'kodein-conf'
                    description 'Kodein extension that enables a configurable Kodein object and a static global one true Kodein'
                    url 'https://github.com/SalomonBrys/Kodein'

                    licenses {
                        license {
                            name 'MIT'
                            url 'http://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id 'salomonbrys'
                            name 'Salomon BRYS'
                            email 'salomon.brys@gmail.com'
                        }
                    }

                    scm {
                        url 'https://github.com/SalomonBrys/Kodein'
                        connection 'scm:git:https://github.com/SalomonBrys/Kodein.git'
                        developerConnection 'scm:git:git@github.com:SalomonBrys/Kodein.git'
                        tag 'HEAD'
                    }

                    issueManagement {
                        system 'GitHub Issues'
                        url 'https://github.com/SalomonBrys/Kodein/issues'
                    }
                }
            }
        }
    }
}
